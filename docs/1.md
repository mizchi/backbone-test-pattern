# What is testable Backbone?

This repo is backbone test guide for my company, Quipper.

It is aimed to introduce `Modulable` and `Testable` backbone frontend by this guide.


Built by ...

- backbone
- mocha
- sinon
- chai
- mocha-phantomjs
- browserify
- grunt

All components are optional and use your favorite library. But I strongly recommend `mocha`, `sinon` and `chai`'s `expect` here.


## Principles

- Don't touch global element, like a window or document!
- Don't use complex jQuery selector
- All components do those roll by themselves.
- Extend Backbone for your way


## Extend Backbone

Backbone is very this framework. It doesn't prepare development way. It is only backbone, exactly as stated.

So we inject our way to base view. See `app/views/base/view.coffee`.

```coffeescript
module.exports = class View extends Backbone.View
  template: ->
  initialize: (options) ->
    if options?.model instanceof Backbone.Model
      @model = options.model
    else
      @model = new Backbone.Model
      if options?.model?
        @model.set(options.model)
    @render()

  render: ->
    @$el.html @template(@model.toJSON())

  attach: (selector) ->
    $(selector).append @$el
```

Backbone is extendable by CoffeeScript class extend way. (Both are developped by @jashkenas)

By this test, I ensure template expandion and attach utility. I want to say is I can test without body and document. Global DOM is singleton and using it makes test difficult.

`Backbone.View#initialize` is called by its constructor and before `delegateEvents`. Take care. This is used for DOM setup and not on any element.
In generally, `render` function is used to inject html to `@$el` element.

I added auto Backbone.Model instantiation unless model argument as `model` property. if it is raw object, It was used as Backbone.Model's argument.


And I write this test `test/views/base/view-test.coffee`

```coffeescript
View = require '../../../app/views/base/view.coffee'
template = require './template.hbs'

describe 'View', ->
  describe '#constructor', ->
    class XView extends View
      template: template

    context 'with model property x is foo', ->
      beforeEach ->
        @object = x: 'foo'
      it 'should render by handlebars with model properties', ->
        xview = new XView model: @object
        expect(xview.$el.html()).eq '<div id="x">foo</div>'

  describe '#attach', ->
    it 'should append itself to jQuery element', ->
      $sandbox = $('<div>')
      view = new View
      view.attach($sandbox)
      expect($sandbox).to.not.have.html ''
```

Now I use `hbsfy` to handle handlebars template. `require('~.hbs')` returns precompiled function. You can use your favorite template and setup this for `render` function.


## Compose standalone module

Backbone.View has `$el` as jQuery element. `$('<div>')` is default. To render html template into it, it can behave itselt perfectly.

Test and code correspond 1 to 1.

See `app/views/content.coffee` and `test/views/content-test.coffee`

```
View = require './base/view.coffee'
template = require '../templates/content.hbs'

module.exports = class ContentView extends View
  template: template
  events:
    'click .js-show-content': 'showContent'
    'click .js-hide-content': 'hideContent'
    'click .js-save-content': 'saveContent'

  showContent: ->
    @$('.content').show()

  hideContent: ->
    @$('.content').hide()

  saveContent: ->
    localStorage.setItem 'content', @model.get 'content'
```

(It is meaningless code for explanation)

ContentView extends View and by `events` handle `click` message.
To restrict DOM access only @$, This class ensure not touches to global DOM.


```coffee
ContentView = require '../../app/views/content-view.coffee'

describe 'ContentView', ->

  it 'should request content foo', ->
    contentView = new ContentView model: {content: 'foo'}
    expect(contentView.$('.content')).to.have.text('foo')

  describe '#showContent', ->
    it 'should show content', ->
      content = new ContentView
      content.$('.js-show-content').click()
      expect(content.$('.content').css('display')).eq 'block'

  describe '#hideContent', ->
    it 'should hide content', ->
      content = new ContentView
      content.$('.js-hide-content').click()
      expect(content.$('.content').css('display')).eq 'none'

  describe '#saveContent', ->
    beforeEach ->
      @setItemSpy = sinon.stub(localStorage,'setItem')

    afterEach ->
      @setItemSpy.restore()

    it 'should save content to localStorage', ->
      content = new ContentView model: {content: 'tosave'}
      content.saveContent()
      expect(@setItemSpy.calledWith(sinon.match.string)).to.be.true
```

Using jQuery complecated becomes spaghetti, but at test jQuery is your friend. It provides useful utility.

To ensure click and method call, `@$(selector).click()` can reproduce user click aciton. And to know DOM state, it can be used like this.

```coffee
it 'should hide content', ->
  content = new ContentView
  content.$('.js-hide-content').click()
  expect(content.$('.content').css('display')).eq 'none'
```

`trigger('click')` is same.

(Unfortunately, this is a bit bad case. Because we had to know jQuery#hide occur display:none at first)

(Someone please teach me ugly `../..` to get `app` dir by browserify)


## Network testing

See `app/models/content.coffee` and `test/models/content-test.coffee`

```
module.exports = class Content extends Backbone.Model
  urlRoot: '/content'
```

This Classe's `fetch` request to '/content'

Content = require '../../app/models/content.coffee'

```coffee
describe 'Content', ->
  stubRequest
    url: "/content"

  describe '#fetch', ->
    it 'should quest callback ', (done) ->
      content = new Content
      content.fetch().done -> done()
```

This is asynchronous testing.

`stubRequest` is sinon.fakeServer wrapper defined at `test/helpers.coffee`

```coffee
window.stubRequest = ({url, method, status, response}) ->
  beforeEach ->
    throw 'url is required' unless url
    method ?= 'GET'
    status ?= 200
    response ?= {}

    unless @_server
      @_server = sinon.fakeServer.create()
      @_server.autoRespond = true

    @_server.respondWith(method, url,
      [
        status
        {"Content-Type": "application/json"}
        JSON.stringify(response)
      ]
    )

  afterEach: ->
    @_server?.restore?()
    delete @_server
```


## What we can up to this point

- Network testing
- DOM action
- ... wihout global dependencies

## We have to add controller test?

Controller test is bit difficult. If we need to this, prepare Backbone.Router and need some hacks.
But I think this is difficult but low merit to application. Controller test is nealy end to end testing and ensure application by doint e2e.

